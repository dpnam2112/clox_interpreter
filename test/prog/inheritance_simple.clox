print "== Test: simple inheritance ==";
class Person {
  init(name, age) {
    this.name = name;
    this.age = age;
  }

  getName() {
    return this.name;
  }

  getAge() {
    return this.age;
  }
}

class Engineer < Person {
  init(name, age, title) {
    super.init(name, age);
    this.title = title;
  }

  getTitle() {
    return this.title;
  }
}

var bob = Engineer("Bob Patterson", 25, "Senior electronic engineer");
// try accessing fields
print bob.name;
print bob.title;

// try invoking methods
print bob.getName();
print bob.getAge();
print bob.getTitle();

print "== Test: inheritance inside scope ==";
// the class Student_ should be captured inside context of each method as an upvalue.
var CollegeStudent = nil;
{
  class Student_ < Person {
    init(name, age, level) {
      super.init(name, age);
      this.level = level;
    }

    getLevel() {
      return this.level;
    }
  }

  class CollegeStudent_ < Student_ {
    init(name, age, major) {
      super.init(name, age, "College");
      this.major = major;
    }

    getMajor() {
      return this.major;
    }
  }

  var alice = CollegeStudent_("Alice Patterson", 23, "Electronic engineering");
  print alice.getName();
  print alice.getAge();
  print alice.getLevel();
  print alice.getMajor();

  CollegeStudent = CollegeStudent_;
}

var harry = CollegeStudent("Harry Patterson", 23, "Computer engineering");
print harry.getName();
print harry.getAge();
print harry.getLevel();
print harry.getMajor();

print "== Test: bound method ==";
{
  class Foo < Person {
    printInfo() {
      var superGetName = super.getName;
      var superGetAge = super.getAge;
      print superGetName();
      print superGetAge();
    }

    foo(a, b, c) {
      return a + b + c;
    }
  }

  var foo = Foo("Foo", 17);
  foo.printInfo();

  class Bar < Foo {

  }

  class Baz < Bar {
    getName_() {
      var superGetName = super.getName;
      return superGetName();
    }
  }

  var baz = Baz("Baz", 17);
  print baz.getName_();
  print baz.foo(1, 2, 3);
}
